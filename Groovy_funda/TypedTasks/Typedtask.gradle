/*
    Typed Tasks are configuring tasks to copy and delete
    files as well as build archives.

    Exercises:

    1. Create a task named "copyWeb". This task should perform the following:
        a) Copy files from "src/web" to a folder "build/web".
        b) Copy all .txt files from "src/docs" to folder "build/web/help".

    2. Create a task named "bundleWeb". This task should perform the following:
        a) Create a zip file located in the "build" folder named "web.zip".
        b) Include all the files copied by the "copyWeb" task except the
        "images" directory.

    3. Create a task named "unpackBundle". This task should perform the following:
        a) Expand the zip file created by the "bundleWeb" task.
        b) Copy the contents to a folder "build/exploded".

    HINT: There isn't an UnZip task, so what task type do you need?

    4. Create a task named "deleteHelp". This task should perform the following:
        a) Delete the "help" folder created by the "copyWeb" task.
*/

// Solution 1.
task copyWeb(type: Copy){
    from 'src/web'

    from('src/docs'){
        include '*.txt'
        into 'help'
    }
    into 'build/web'
}

// Solution 2.
task bundleWeb(type: Zip, dependsOn: copyWeb){
    from('build/web'){
        exclude 'images'
    }
    baseName = 'web'
    destinationDir = file('build')
}

// Solution 3.
task unpackBundle(type:Copy, dependsOn:bundleWeb){
    from zipTree('build/web.zip')
    into 'build/exploded'
}

// Solution 4.
task deleteHelp(type: Delete, dependsOn: copyWeb) {
    delete 'build/web/help'
}
